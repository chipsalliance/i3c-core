// I3C controller register map based on MIPI HCI 1.2


regfile TargetTransactionInterfaceRegisters #(
    bit rx_desc_fifo_size,
    bit tx_desc_fifo_size,
    bit rx_fifo_size,
    bit tx_fifo_size,
    bit ibi_fifo_size
){
    name = "Target Transaction Interface";
    default regwidth = 32;
    reg {
        regwidth = 32;
        field {
            name = "CAP_LENGTH";
            desc = "Capability Structure Length in DWORDs";
            sw = r;
            hw = r;
            reset = 16'h0010;
        } CAP_LENGTH[23:8];
        field {
            name = "CAP_ID";
            desc = "Extended Capability ID";
            sw = r;
            hw = r;
            reset = 8'hC4;
        } CAP_ID[7:0];
    } EXTCAP_HEADER;
    reg {
        name = "TTI Control";
        desc = "Control Register";
        field {
            name = "IBI_RETRY_NUM";
            desc = "Number of times the Target Device will try to request an IBI before giving up.

            Values:

            0x0 - Device will never retry.

            0x1-0x6 - Device will retry this many times.

            0x7 - Device will retry indefinitely until the Active Controller sets
            DISINT bit in the DISEC command.
            ";
            sw = rw;
            hw = r;
            reset = 3'b000;
        } IBI_RETRY_NUM[15:13];
        field {
            name = "IBI_EN";
            desc = "Enable the IBI queue servicing.

            Values:

            0x0 - Device will not service the IBI queue.

            0x1 - Device will send IBI requests onto the bus, if possible.
            ";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b1;
        } IBI_EN[12:12];
        field {
            name = "CRR_EN";
            desc = "Enable Controller Role Request.

            Values:

            0x0 - Device is allowed to perform Controller Role Request.

            0x1 - Device is not allowed to perform Controller Role Request.
            ";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } CRR_EN[11:11];
        field {
            name = "HJ_EN";
            desc = "Enable Hot-Join capability.

            Values:

            0x0 - Device is allowed to attempt Hot-Join.

            0x1 - Device is not allowed to attempt Hot-Join.
            ";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b1;
        } HJ_EN[10:10];
    } CONTROL;
    reg {
        name = "TTI Status";
        desc = "Status Register";
        field {
            name = "LAST_IBI_STATUS";
            desc = "Status of last IBI. Should be read after IBI_DONE interrupt.

            Values:

            00 - Success: IBI was transmitted and ACK'd by the Active Controller.
            01 - Failure: Active Controller NACK'd the IBI before any data was sent.
            The Target Device will retry sending the IBI once.
            10 - Failure: Active Controller NACK'd the IBI after partial data was sent.
            Part of data in the IBI queue is considered corrupted and will be discarded.
            11 - Failure: IBI was terminated after 1 retry.
            ";
            sw = r;
            hw = rw;
            we = true;
            reset = 2'b00;
            swacc = true;
        } LAST_IBI_STATUS[15:14];
        field {
            name = "PROTOCOL_ERROR";
            desc = "Protocol error occurred in the past. This field can only be reset
            by the Controller, if it issues the GETSTATUS CCC.

            Values:

            0 - no error occurred

            1 - generic protocol error occurred in the past. It will be set until reception
            of the next GETSTATUS command.
            ";
            sw = r;
            hw = rw;
            reset = 1'b0;
        } PROTOCOL_ERROR[13:13];
    } STATUS;
    reg {
        name = "TTI Queue Reset Control";
        desc = "Queue Reset Control";
        field {
            name = "IBI_QUEUE_RST";
            desc = "TTI IBI Queue Buffer Software Reset";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } IBI_QUEUE_RST[5:5];
        field {
            name = "RX_DATA_RST";
            desc = "TTI RX Data Queue Buffer Software Reset";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } RX_DATA_RST[4:4];
        field {
            name = "TX_DATA_RST";
            desc = "TTI TX Data Queue Buffer Software Reset";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } TX_DATA_RST[3:3];
        field {
            name = "RX_DESC_RST";
            desc = "TTI RX Descriptor Queue Buffer Software Reset";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } RX_DESC_RST[2:2];
        field {
            name = "TX_DESC_RST";
            desc = "TTI TX Descriptor Queue Buffer Software Reset";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } TX_DESC_RST[1:1];
        field {
            name = "SOFT_RST";
            desc = "Target Core Software Reset";
            sw = rw;
            hw = rw;
            we = true;
            reset = 1'b0;
        } SOFT_RST[0:0];
    } RESET_CONTROL;
    reg {
        name = "TTI Interrupt Status";
        desc = "Interrupt Status";
        field {
            name = "TRANSFER_ERR_STAT";
            desc = "Bus error occurred";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TRANSFER_ERR_STAT[31:31];
        field {
            name = "TX_DESC_COMPLETE";
            desc = "Read Transaction on the I3C Bus completede";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TX_DESC_COMPLETE[26:26];
        field {
            name = "TRANSFER_ABORT_STAT";
            desc = "Bus aborted transaction";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TRANSFER_ABORT_STAT[25:25];
        field {
            name = "PENDING_INTERRUPT";
            desc = "Contains the interrupt number of any pending interrupt, or 0 if no interrupts are pending. This encoding allows for up to 15 numbered interrupts. If more than one interrupt is set, then the highest priority interrupt shall be returned.";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 4'd0;
        } PENDING_INTERRUPT[18:15];
        field {
            name = "IBI_DONE";
            desc = "IBI is done, check LAST_IBI_STATUS for result.";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } IBI_DONE[13:13];
        field {
            name = "IBI_THLD_STAT";
            desc = "TTI IBI Buffer Threshold Status, the Target Controller shall set this bit to 1 when the number of available entries in the TTI IBI Queue is >= the value defined in `TTI_IBI_THLD`";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } IBI_THLD_STAT[12:12];
        field {
            name = "RX_DESC_THLD_STAT";
            desc = "TTI RX Descriptor Buffer Threshold Status, the Target Controller shall set this bit to 1 when the number of available entries in the TTI RX Descriptor Queue is >= the value defined in `TTI_RX_DESC_THLD`";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } RX_DESC_THLD_STAT[11:11];
        field {
            name = "TX_DESC_THLD_STAT";
            desc = "TTI TX Descriptor Buffer Threshold Status, the Target Controller shall set this bit to 1 when the number of available entries in the TTI TX Descriptor Queue is >= the value defined in `TTI_TX_DESC_THLD`";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TX_DESC_THLD_STAT[10:10];
        field {
            name = "RX_DATA_THLD_STAT";
            desc = "TTI RX Data Buffer Threshold Status, the Target Controller shall set this bit to 1 when the number of entries in the TTI RX Data Queue is >= the value defined in `TTI_RX_DATA_THLD`";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } RX_DATA_THLD_STAT[9:9];
        field {
            name = "TX_DATA_THLD_STAT";
            desc = "TTI TX Data Buffer Threshold Status, the Target Controller shall set this bit to 1 when the number of available entries in the TTI TX Data Queue is >= the value defined in `TTI_TX_DATA_THLD`";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TX_DATA_THLD_STAT[8:8];
        field {
            name = "TX_DESC_TIMEOUT";
            desc = "Pending Read was NACK’ed, because the `TX_DESC_STAT` event was not handled in time";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TX_DESC_TIMEOUT[3:3];
        field {
            name = "RX_DESC_TIMEOUT";
            desc = "Pending Write was NACK’ed, because the `RX_DESC_STAT` event was not handled in time";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } RX_DESC_TIMEOUT[2:2];
        field {
            name = "TX_DESC_STAT";
            desc = "There is a pending Read Transaction on the I3C Bus. Software should write data to the TX Descriptor Queue and the TX Data Queue";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } TX_DESC_STAT[1:1];
        field {
            name = "RX_DESC_STAT";
            desc = "There is a pending Write Transaction. Software should read data from the RX Descriptor Queue and the RX Data Queue";
            sw = rw;
            hw = rw;
            woclr = true;
            we = true;
            reset = 1'b0;
        } RX_DESC_STAT[0:0];
    } INTERRUPT_STATUS;
    reg {
        name = "TTI Interrupt Enable";
        desc = "Interrupt Enable";
        field {
            name = "TRANSFER_ERR_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TRANSFER_ERR_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TRANSFER_ERR_STAT_EN[31:31];
        field {
            name = "TX_DESC_COMPLETE_EN";
            desc = "Enables the corresponding interrupt bit `TX_DESC_COMPLETE_EN`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_COMPLETE_EN[26:26];
        field {
            name = "TRANSFER_ABORT_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TRANSFER_ABORT_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TRANSFER_ABORT_STAT_EN[25:25];
        field {
            name = "IBI_DONE_EN";
            desc = "Enables the corresponding interrupt bit `IBI_DONE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } IBI_DONE_EN[13:13];
        field {
            name = "IBI_THLD_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TTI_IBI_THLD_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } IBI_THLD_STAT_EN[12:12];
        field {
            name = "RX_DESC_THLD_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TTI_RX_DESC_THLD_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DESC_THLD_STAT_EN[11:11];
        field {
            name = "TX_DESC_THLD_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TTI_TX_DESC_THLD_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_THLD_STAT_EN[10:10];
        field {
            name = "RX_DATA_THLD_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TTI_RX_DATA_THLD_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DATA_THLD_STAT_EN[9:9];
        field {
            name = "TX_DATA_THLD_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TTI_TX_DATA_THLD_STAT`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DATA_THLD_STAT_EN[8:8];
        field {
            name = "TX_DESC_TIMEOUT_EN";
            desc = "Enables the corresponding interrupt bit `TX_DESC_TIMEOUT_EN`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_TIMEOUT_EN[3:3];
        field {
            name = "RX_DESC_TIMEOUT_EN";
            desc = "Enables the corresponding interrupt bit `RX_DESC_TIMEOUT_EN`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DESC_TIMEOUT_EN[2:2];
        field {
            name = "TX_DESC_STAT_EN";
            desc = "Enables the corresponding interrupt bit `TX_DESC_STAT_EN`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_STAT_EN[1:1];
        field {
            name = "RX_DESC_STAT_EN";
            desc = "Enables the corresponding interrupt bit `RX_DESC_STAT_EN`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DESC_STAT_EN[0:0];
    } INTERRUPT_ENABLE;
    reg {
        name = "TTI Interrupt Force";
        desc = "Interrupt Force";
        field {
            name = "TRANSFER_ERR_STAT_FORCE";
            desc = "Enables the corresponding interrupt bit `TRANSFER_ERR_STAT_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TRANSFER_ERR_STAT_FORCE[31:31];
        field {
            name = "TX_DESC_COMPLETE_FORCE";
            desc = "Enables the corresponding interrupt bit `TX_DESC_COMPLETE_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_COMPLETE_FORCE[26:26];
        field {
            name = "TRANSFER_ABORT_STAT_FORCE";
            desc = "Enables the corresponding interrupt bit `TRANSFER_ABORT_STAT_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TRANSFER_ABORT_STAT_FORCE[25:25];
        field {
            name = "IBI_DONE_FORCE";
            desc = "Enables the corresponding interrupt bit `IBI_DONE_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } IBI_DONE_FORCE[13:13];
        field {
            name = "IBI_THLD_FORCE";
            desc = "Forces the corresponding interrupt bit `TTI_IBI_THLD_STAT` to be set to 1";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } IBI_THLD_FORCE[12:12];
        field {
            name = "RX_DESC_THLD_FORCE";
            desc = "Forces the corresponding interrupt bit `TTI_RX_DESC_THLD_STAT` to be set to `1`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DESC_THLD_FORCE[11:11];
        field {
            name = "TX_DESC_THLD_FORCE";
            desc = "Forces the corresponding interrupt bit `TTI_TX_DESC_THLD_STAT` to be set to `1`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_THLD_FORCE[10:10];
        field {
            name = "RX_DATA_THLD_FORCE";
            desc = "Forces the corresponding interrupt bit `TTI_RX_DATA_THLD_STAT` to be set to `1`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DATA_THLD_FORCE[9:9];
        field {
            name = "TX_DATA_THLD_FORCE";
            desc = "Forces the corresponding interrupt bit `TTI_TX_DATA_THLD_STAT` to be set to `1`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DATA_THLD_FORCE[8:8];
        field {
            name = "TX_DESC_TIMEOUT_FORCE";
            desc = "Enables the corresponding interrupt bit `TX_DESC_TIMEOUT_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_TIMEOUT_FORCE[3:3];
        field {
            name = "RX_DESC_TIMEOUT_FORCE";
            desc = "Enables the corresponding interrupt bit `RX_DESC_TIMEOUT_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DESC_TIMEOUT_FORCE[2:2];
        field {
            name = "TX_DESC_STAT_FORCE";
            desc = "Enables the corresponding interrupt bit `TX_DESC_STAT_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } TX_DESC_STAT_FORCE[1:1];
        field {
            name = "RX_DESC_STAT_FORCE";
            desc = "Enables the corresponding interrupt bit `RX_DESC_STAT_FORCE`";
            sw = rw;
            hw = r;
            reset = 1'b0;
        } RX_DESC_STAT_FORCE[0:0];
    } INTERRUPT_FORCE;
    external reg {
        name = "TTI RX Descriptor Queue Port";
        desc = "RX Descriptor Queue Port";
        field {
            name = "RX_DESC";
            desc = "RX Data";
            sw = r;
            hw = w;
            reset = 32'h00000000;
        } RX_DESC[31:0];
    } RX_DESC_QUEUE_PORT;
    external reg {
        name = "TTI RX Data Port";
        desc = "RX Data Port";
        field {
            name = "RX_DATA";
            desc = "RX Data";
            sw = r;
            hw = w;
            reset = 32'h00000000;
        } RX_DATA[31:0];
    } RX_DATA_PORT;
    external reg {
        name = "TTI TX Descriptor Queue Port";
        desc = "TX Descriptor Queue Port";
        field {
            name = "TX_DESC";
            desc = "TX Data";
            sw = w;
            hw = r;
            reset = 32'h00000000;
        } TX_DESC[31:0];
    } TX_DESC_QUEUE_PORT;
    external reg {
        name = "TTI TX Data Port";
        desc = "TX Data Port";
        field {
            name = "TX_DATA";
            desc = "TX Data";
            sw = w;
            hw = r;
            reset = 32'h00000000;
        } TX_DATA[31:0];
    } TX_DATA_PORT;
    external reg {
        name = "TTI IBI Data Port";
        desc = "IBI Data Port";
        field {
            name = "IBI_DATA";
            desc = "IBI Data";
            sw = w;
            hw = r;
            reset = 32'h00000000;
        } IBI_DATA[31:0];
    } IBI_PORT;
    reg {
        name = "TTI Queue Size";
        desc = "Queue Size";
        field {
            name = "TX_DATA_BUFFER_SIZE";
            desc = "Transmit Data Buffer Size in DWORDs calculated as `2^(N+1)`";
            sw = r;
            hw = r;
            reset = tx_fifo_size;
        } TX_DATA_BUFFER_SIZE[31:24];
        field {
            name = "RX_DATA_BUFFER_SIZE";
            desc = "Receive Data Buffer Size in DWORDs calculated as `2^(N+1)`";
            sw = r;
            hw = r;
            reset = rx_fifo_size;
        } RX_DATA_BUFFER_SIZE[23:16];
        field {
            name = "TX_DESC_BUFFER_SIZE";
            desc = "TX Descriptor Buffer Size in DWORDs calculated as `2^(N+1)`";
            sw = r;
            hw = r;
            reset = tx_desc_fifo_size;
        } TX_DESC_BUFFER_SIZE[15:8];
        field {
            name = "RX_DESC_BUFFER_SIZE";
            desc = "RX Descriptor Buffer Size in DWORDs calculated as `2^(N+1)`";
            sw = r;
            hw = r;
            reset = rx_desc_fifo_size;
        } RX_DESC_BUFFER_SIZE[7:0];
    } QUEUE_SIZE;
    reg {
        name = "TTI IBI Queue Size";
        desc = "IBI Queue Size";
        field {
            name = "IBI_QUEUE_SIZE";
            desc = "IBI Queue Size in DWORDs calculated as `2^(N+1)`";
            sw = r;
            hw = r;
            reset = ibi_fifo_size;
        } IBI_QUEUE_SIZE[7:0];
    } IBI_QUEUE_SIZE;
    reg {
        name = "TTI Queue Threshold Control";
        desc = "Queue Threshold Control";
        field {
            name = "IBI_THLD";
            desc = "Controls the minimum number of IBI Queue entries needed to trigger the IBI threshold interrupt.";
            sw = rw;
            hw = rw;
            reset = 8'h01;
            swmod = true;
            we = true;
        } IBI_THLD[31:24];
        field {
            name = "RX_DESC_THLD";
            desc = "Controls the minimum number of TTI RX Descriptor Queue entries needed to trigger the TTI RX Descriptor interrupt.";
            sw = rw;
            hw = rw;
            reset = 8'h01;
            swmod = true;
            we = true;
        } RX_DESC_THLD[15:8];
        field {
            name = "TX_DESC_THLD";
            desc = "Controls the minimum number of empty TTI TX Descriptor Queue entries needed to trigger the TTI TX Descriptor interrupt.";
            sw = rw;
            hw = rw;
            reset = 8'h01;
            swmod = true;
            we = true;
        } TX_DESC_THLD[7:0];
    } QUEUE_THLD_CTRL;
    reg {
        name = "TTI IBI Queue Threshold Control";
        desc = "IBI Queue Threshold Control";
        field {
            name = "RX_DATA_THLD";
            desc = "Minimum number of TTI RX Data queue entries of data received, in DWORDs, that will trigger the TTI RX Data interrupt. Interrupt triggers when `2^(N+1)` RX Buffer DWORD entries are received during the Read transfer.";
            sw = rw;
            hw = r;
            reset = 3'h1;
        } RX_START_THLD[26:24];
        field {
            name = "TX_DATA_THLD";
            desc = "Minimum number of available TTI TX Data queue entries, in DWORDs, that will trigger the TTI TX Data interrupt. Interrupt triggers when `2^(N+1)` TX Buffer DWORD entries are available.";
            sw = rw;
            hw = r;
            reset = 3'h1;
        } TX_START_THLD[18:16];
        field {
            name = "RX_DATA_THLD";
            desc = "Minimum number of TTI RX Data queue entries of data received, in DWORDs, that will trigger the TTI RX Data interrupt. Interrupt triggers when `2^(N+1)` RX Buffer DWORD entries are received during the Read transfer.";
            sw = rw;
            hw = r;
            reset = 3'h1;
        } RX_DATA_THLD[10:8];
        field {
            name = "TX_DATA_THLD";
            desc = "Minimum number of available TTI TX Data queue entries, in DWORDs, that will trigger the TTI TX Data interrupt. Interrupt triggers when `2^(N+1)` TX Buffer DWORD entries are available.";
            sw = rw;
            hw = r;
            reset = 3'h1;
        } TX_DATA_THLD[2:0];
    } DATA_BUFFER_THLD_CTRL;
};
